`timescale 1ns / 1ns
//////////////////////////////////////////////////////////////////////////////////
// Engineer:		Rushi Patel 
// 
// Create Date:    16:58:00 09/25/2015 
// Design Name: 	ALU
// Module Name:     ALU 
// Project Name: 	Lab2
// Description:		ALU unit for Pipelined Datapath 
//
// Revision: 
// Revision 0.01 - File Created
// Additional Comments: 
// 		Used localparams for the ALU case options:
// 		Parameterized the Input and Output to be able to use ALU for any
// 		sized values.
//
//////////////////////////////////////////////////////////////////////////////////
module ALU(A, B, Control, Out, Zero);
parameter SIZE = 32;

localparam NOOP = 6'h0;
localparam MOV = 6'h10, ADD = 6'h12, SUB = 6'h13, OR = 6'h14, AND = 6'h15;
localparam BEQ = 6'h20;
localparam ADDI = 6'h32, SUBI = 6'h33, ORI = 6'h34, ANDI = 6'h35;
localparam LI = 6'h39, LWI = 6'h3B, SWI = 6'h3C;

input [SIZE-1:0] A, B;
input [5:0] Control;
output reg [SIZE-1:0] Out;
output reg Zero;

always @(*)
begin
	case (Control)
		NOOP: Out = 0;					//NOOP do nothing
		MOV : Out = A;					//MOV : R1 = R2
		LI, LWI, SWI : Out = B;			//LI  : R1 = IMM
		ADD, ADDI : Out = A + B;		//ADD : R1 = R2 + R3
		SUB, BEQ, SUBI : Out = A - B;	//SUB : R1 = R2 - R3
		OR, ORI  : Out = A | B;			//OR  : R1 = R2 | R3
		AND, ANDI : Out = A & B;		//AND : R1 = R2 & R3
		default : Out = 'bx;			//Default : R1 = x
	endcase
end

always @(*)
begin
	if (Out == 0)
		Zero <= 1;
	else
		Zero <= 0;
end

endmodule
